contract IntegerFacility
  definition MIN
    : integer
    satisfies restriction MIN <= 0

  definition MAX
    : integer
    satisfies restriction 0 < MAX

  math subtype INTEGERMODEL is integer
    exemplar i
    constraint MIN <= i and i <= MAX

  type Integer is modeled by INTEGERMODEL
    exemplar i
    initialization ensures i = 0

  procedure Increment(updates i: Integer)
    requires
      i < MAX
    ensures
      i = #i + 1

  procedure Decrement(updates i: Integer)
    requires
      MIN < i
    ensures
      i = #i - 1

  function AreEqual (restores i: Integer,
                     restores j: Integer) : control
    ensures
      AreEqual = (i = j)

  function IsGreater (restores i: Integer,
                      restores j: Integer) : control
    ensures
      IsGreater = (i > j)

  function Replica (restores i: Integer) : Integer
    ensures
      Replica = i

  function Min () : Integer
    ensures
      Min = MIN

  function Max () : Integer
    ensures
      Max = MAX

end IntegerFacility
