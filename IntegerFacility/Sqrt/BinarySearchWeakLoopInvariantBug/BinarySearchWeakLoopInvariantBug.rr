realization BinarySearchWeakLoopInvariantBug
  implements Sqrt for IntegerFacility

  uses Add for IntegerFacility
  uses Subtract for IntegerFacility
  uses Multiply for IntegerFacility
  uses Divide for IntegerFacility
  uses Square for IntegerFacility

  procedure Sqrt (updates i: Integer)
    variable one: Integer
    Increment(one)
    if IsGreater(i, one) then
      variable two, t, hi, d: Integer
      Increment(two)
      Increment(two)
      t := Replica(i)
      hi := Replica(i)
      Increment(hi)
      Clear(i)
      d := Replica(hi)
      loop
        maintains 0 <= i and i * i <= t and
                  t < hi * hi and
                  hi <= MAX and
                  d = hi - i and
                  one = #one and
                  two = #two and
                  t = #t
        decreases d
      while IsGreater(d, one) do
        variable m, msq, r: Integer
        m := Replica(i)
        Add(m, hi)
        Divide(m, two, r)
        msq := Replica(m)
        Square(msq)
        if IsGreater(msq, t) then
          hi :=: m
        else
          i :=: m
        end if
        d := Replica(hi)
        Subtract(d, i)
      end loop
    end if
  end Sqrt

end BinarySearchWeakLoopInvariantBug
